
/*
 * Copyright (C) 2005-present, 58.com.  All rights reserved.
 * Use of this source code is governed by a BSD type license that can be
 * found in the LICENSE file.
 */

group 'com.wuba.fair'
version '1.0'

buildscript {
    ext.kotlin_version = '1.3.50'
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: "kotlin-android"

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 19
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    //java版本8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    packagingOptions {
        exclude 'lib/armeabi-v7a/libj2v8.so'
        exclude 'lib/arm64-v8a/libj2v8.so'
        exclude 'lib/x86/libj2v8.so'
        exclude 'lib/x86_64/libj2v8.so'
    }

    buildTypes {
        release {
            consumerProguardFiles 'proguard-rules.pro'
            externalNativeBuild {
                cmake {
                    cppFlags "-DTENCENT_SHELL=1", "-DNOLOG=1"
                }
            }
            buildConfigField "boolean", "IS_RELEASE", "true"
        }
        debug {
            externalNativeBuild {
                cmake {
                    cppFlags "-DTENCENT_SHELL=0"
                }
            }
            buildConfigField "boolean", "IS_RELEASE", "false"
        }
    }

    sourceSets {
        main {
            java {
                srcDirs = [
                        'src/main/java',
                        'src/main/v8',
                ]
            }
        }
        test.java.srcDirs 'src/test/java', 'src/testV8/java'
    }

}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

